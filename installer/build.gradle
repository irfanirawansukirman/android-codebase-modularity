apply from: "$rootDir/build-systems/dependencies.gradle"

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion appConfig.sdkTarget
    buildToolsVersion appConfig.buildToolsV

    defaultConfig {
        applicationId appConfig.appId
        minSdkVersion appConfig.sdkMin
        targetSdkVersion appConfig.sdkTarget
        versionCode appConfig.buildNumber
        versionName appConfig.nameVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions appConfig.flavorMode
    productFlavors {
        dev {
            dimension appConfig.flavorMode
            applicationIdSuffix appConfig.idSuffixDev
            resValue "string", "app_name", appConfig.nameTitleDev
        }
        prod {
            dimension appConfig.flavorMode
            resValue "string", "app_name", appConfig.nameTitle
        }
    }

    testOptions {
        unitTests.includeAndroidResources true
        unitTests.returnDefaultValues = true
    }

    signingConfigs {
        release {

        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavor = variant.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def appTitle = 'your app name'
            def appBuildDate = new Date().format("ddMMyyyy")
            outputFileName = "${appTitle}-${flavor}-${versionName}#${versionCode}-${appBuildDate}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation kotlinLibrary.languagePluginX
    implementation androidSupportLibrary.appCompatX

    /**
     * Airbnb deeplink
     */
    implementation utils.deeplinkDispatch
    kapt utils.deeplinkProcessor

    /**
     * Data n' Domain Put Here
     */
    implementation project(":domain")
    implementation project(":data")

    /**
     * Base n' Common Put Here
     */
    implementation project(":libraries:abstraction")

    implementation diLibrary.koinAndoid
    implementation diLibrary.koinVM

    /**
     * Feature Module Put Here
     */
    implementation project(":features:home")
    implementation project(":features:chat")
}

apply from: "$rootDir/build-systems/feature-test-depends.gradle"
