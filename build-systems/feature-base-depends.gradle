apply from: "$rootDir/build-systems/dependencies.gradle"

apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
    maven { url 'https://maven.google.com' }
}

android {
    compileSdkVersion 29
    buildToolsVersion appConfig.buildToolsV

    defaultConfig {
        applicationId appConfig.appId
        minSdkVersion appConfig.sdkMin
        targetSdkVersion appConfig.sdkTarget
        versionCode appConfig.buildNumber
        versionName appConfig.nameVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [
                onesignal_app_id               : '0a5af3f3-14d0-4138-a9ee-aec1546c1d51',
                // Project number pulled from dashboard, local value is ignored.
                onesignal_google_project_number: 'REMOTE'
        ]

        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias "lazio12345"
            keyPassword "lazio12345"
            storeFile file("lazio12345.jks")
            storePassword "lazio12345"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            debuggable false

            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            testCoverageEnabled true
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions appConfig.flavorMode
    productFlavors {
        dev {
            dimension appConfig.flavorMode
            applicationIdSuffix appConfig.idSuffixDev
            resValue "string", "app_name", appConfig.nameTitleDev
        }
        prod {
            dimension appConfig.flavorMode
            resValue "string", "app_name", appConfig.nameTitle
        }
    }

    testOptions {
        animationsDisabled true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavor = variant.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def appTitle = 'MyApp'
            def appBuildDate = new Date().format("ddMMyyyy")
            outputFileName = "${appTitle}-${flavor}-${versionName}#${versionCode}-${appBuildDate}.apk"
        }
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation kotlinLibrary.languagePluginX
    implementation kotlinLibrary.extCoreX

    implementation androidSupportLibrary.appCompatX
    implementation androidSupportLibrary.recyclerX

    implementation aacLibrary.lifecycleVmExt

    implementation project(":domain")
    implementation project(":data")

    implementation project(":libraries:abstraction")
    implementation project(":libraries:medsocauth")

    implementation project(":features:home")
    implementation project(":features:chat")

    implementation diLibrary.koinAndoid
    implementation diLibrary.koinVM

    implementation firebaseLibrary.fbAnal
    implementation firebaseLibrary.fbCrash

    implementation pushNotification.oneSignal

    implementation debugging.stethoDebug

    implementation utils.supportMultidex
    implementation utils.dexterPermission
    implementation utils.deeplinkDispatch
    kapt utils.deeplinkProcessor

    implementation 'com.google.firebase:firebase-config:19.1.3'
}

apply from: "$rootDir/build-systems/feature-test-depends.gradle"

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'